-- @path SWPM=/spdt/src/main/resources/es/uca/spifm/spdt/deployment/swpm/SoftwareWorkProductModel.ecore
-- @path UMA=/spdt/src/main/resources/es/uca/spifm/spdt/process/uma/uma104.ecore


module uma2wspm;
create output: SWPM from input: UMA;

--- Matching MethodPlugin with Project
rule MethodPlugin2Project {
 from
  mp: UMA!MethodPlugin
 to
  proj: SWPM!Project (
   name <- mp.name,
   description <- mp.briefDescription,
   workproducts <- UMA!Domain.allInstances()
  )
}

--- Matching Deliverable with DeliverableProduct
rule Deliverable2DeliverableProduct {
 from
  del: UMA!Deliverable
 to
  dp: SWPM!DeliverableProduct (
   name <- del.presentationName,
   description <- del.briefDescription
  )
}

--- Matching Domain with DocumentaryWorkProduct and Section
rule Domain2DocumentaryWorkProductAndSection {
 from
  domain: UMA!Domain
 to
  dwp: SWPM!DocumentaryWorkProduct (
   name <- domain.presentationName,
   description <- domain.briefDescription,
   sections <- sec
  ),
  sec: SWPM!Section (
   name <- 'Default Section',
   order <- 0,
   description <- 'Container of models',
   models <- domain.workProducts
  )
}

--- Matching Artifact with Model
rule Artifact2Model {
 from
  art: UMA!Artifact (
   art.presentationName.size() > 0
  )
 to
  model: SWPM!Model (
   name <- art.presentationName,
   description <- art.briefDescription,
   type <- #CLASS
  )
}
